/*
⠀⠀⠀⠀⠀⠀⠀⠀⢀⣞⣆⢀⣠⢶⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⢀⣀⡤⠤⠖⠒⠋⠉⣉⠉⠹⢫⠾⣄⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⢠⡏⢰⡴⠀⠀⠀⠉⠙⠟⠃⠀⠀⠀⠈⠙⠦⣄⡀⢀⣀⣠⡤⠤⠶⠒⠒⢿⠋⠈⠀⣒⡒⠲⠤⣄⡀⠀⠀⠀⠀⠀⠀
⢸⠀⢸⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠉⠀⠴⠂⣀⠀⠀⣴⡄⠉⢷⡄⠚⠀⢤⣒⠦⠉⠳⣄⡀⠀⠀⠀
⠸⡄⠼⠦⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣄⡂⠠⣀⠐⠍⠂⠙⣆⠀⠀
⠀⠙⠦⢄⣀⣀⣀⣀⡀⠀⢷⠀⢦⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠰⡇⠠⣀⠱⠘⣧⠀
⠀⠀⠀⠀⠀⠀⠀⠈⠉⢷⣧⡄⢼⠀⢀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠀⡈⠀⢄⢸⡄
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⣿⡀⠃⠘⠂⠲⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⠀⡈⢘⡇
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⢫⡑⠣⠰⠀⢁⢀⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠁⣸⠁
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⣯⠂⡀⢨⠀⠃⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡆⣾⡄⠀⠀⠀⠀⣀⠐⠁⡴⠁⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⣧⡈⡀⢠⣧⣤⣀⣀⡀⢀⡀⠀⠀⢀⣼⣀⠉⡟⠀⢀⡀⠘⢓⣤⡞⠁⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢺⡁⢁⣸⡏⠀⠀⠀⠀⠁⠀⠉⠉⠁⠹⡟⢢⢱⠀⢸⣷⠶⠻⡇⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢈⡏⠈⡟⡇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠑⢄⠁⠀⠻⣧⠀⠀⣹⠁⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣀⣀⡤⠚⠃⣰⣥⠇⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⣾⠼⢙⡷⡻⠀⡼⠁⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠟⠿⡿⠕⠊⠉⠀⠀⠀⠀⠀⠀⠀⠀⣠⣴⣶⣾⠉⣹⣷⣟⣚⣁⡼⠁⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠙⠋⠁⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
*/

// PART 1 - JUNGLE INTRO (Capybara)

// INSTRUCTIONS:

// 0) SET BPM TO 65, Load SCLOrk Samples
// ~bpm.(65) // <- to set the bpm

// 1) Start with the 'RAIN' Ndef, make sure not to go too loud, you can adjust the volume by the .dbamp (the number is negative) so bigger number means less volume.

// 2) After some time turn on the 'WIND' Ndef, same about the volume for this one. We want to make sure sound spreads equally across the room, be heard but not too overpowering.

// 3) Ocasionally play the 'DISTANT THUNDER' Ndef, it will generate a low thunder noise at each click.
// 4) Ocasionally play a random 'JUNGLE SAMPLE' from the sample array below.

// 5) At the time of the loud thunderstrike hit play the 'THUNDER SHOCKWAVE' Ndef and make sure to fade out all other sounds when that happens, you want to be silent after the strike. There is code to silence everything under the thunderstrike shockwave code that should run automatically with it.

// 6) Procced to Part 2 below


// RAIN - 1
(
Ndef(\rain).fadeTime = 20;
Ndef(\rain, {
	var rain, wind, snd;

	rain = {
		Limiter.ar(
			tanh(
				3 * GVerb.ar(
					HPF.ar(
						PinkNoise.ar(0.08+LFNoise1.kr(0.3,0.02))+LPF.ar(Dust2.ar(LFNoise1.kr(0.2).range(40,50)),7000),
						400
					),
					250,100,0.25,drylevel:0.3
				) * Line.kr(0,1,10)
			)
		)
	};

	snd = (rain * -20.dbamp); // <-- adjust volume here if needed

	snd = FreeVerb.ar(in: snd, mix: 0.5, room: 1.0, damp: 0.5);// add some reverb

	snd;
}).play;
)

Ndef(\rain).stop(3);

// WIND - 2
(
Ndef(\wind).fadeTime = 20;
Ndef(\wind, {
	var rain, wind, snd;

	wind = {
		var snd;
		snd = { WhiteNoise.ar } ! 2;
		snd = RLPF.ar(snd, LFNoise2.kr(1).exprange(500, 1000), LFNoise2.kr(2).range(0.5, 0.8));
		snd;
	}.();

	snd = (wind * -25.dbamp); // <-- adjust volume here if needed

	snd = FreeVerb.ar(in: snd, mix: 0.3, room: 1.0, damp: 0.5);// add some reverb

	snd;
}).play;
)

Ndef(\wind).stop(3);


// DISTANT THUNDER - 3 (it can be pretty quiet, try again if you cant hear it)
(
Ndef(\distant_thunder, {
	var noise, env, filterfreq, snd;
	noise = PinkNoise.ar(1);
	filterfreq = LFNoise1.kr(0.2).range(20, 100);
	noise = RLPF.ar(noise, filterfreq, 0.1);
	env = EnvGen.kr(Env.perc(0.01, 3, curve: -4), doneAction:2);
	snd = noise * env * rrand(0.2, 0.6);
	snd = Pan2.ar(Mix(snd), 0.0);
}).play;
)


// JUNGLE SAMPLE - 4
(
// [\birds0, \birds1, \birds2, \birds3, \birds4, \birds5, \birds6, \birds7, \birds8, \birds9]

// choose one of the above to play at a time, try different ones

//~some_sample = d.footsteps; // <-- chagne the sample here
//~some_sample = d.tree; // <-- chagne the sample here
~some_sample = d.birds6; // <-- change the sample here
)
(
Ndef(\junglesample).fadeTime = 0.1;
Ndef(\junglesample, {
	var snd;
	snd = PlayBuf.ar(
		numChannels: ~some_sample.numChannels,
		bufnum: ~some_sample,
		rate: SinOsc.ar(1/30).range(0.5, 1.0) * BufRateScale.kr(~some_sample),
		trigger: Impulse.kr(0),
		startPos: 0.0 * BufSamples.kr(~some_sample),
		loop: 0
	);
	snd = FreeVerb.ar(in: snd, mix: 0.4, room: 0.2, damp: 0.1);

	snd = Pan2.ar(Mix(snd));
	snd = Limiter.ar(snd) * 0.1;
}).play;
)

Ndef(\junglesample).stop(3);


// THUNDER SHOCKWAVE - 5 !!!!!!! PLAY At Thunder Hit
(
Ndef(\thunder_strike_shockwave).fadeTime = 0.3;
Ndef(\thunder_strike_shockwave, {
	var rain, wind, thunder, snd;
	thunder = {
		var trig, snd;
		snd = WhiteNoise.ar;
		snd = Latch.ar(snd, Dust.ar(XLine.kr(8000, 16000, 3)));
		snd = LPF.ar(snd, LFNoise2.kr(8).linexp(-1, 1, 300, 600));
		snd = (snd * 30.dbamp * Env.perc(0.1, 5.0).kr).tanh;
		snd = LPF.ar(snd, 3000);
		snd = snd * Env([0, 1, 0.5, 1, 0], [0.01, 0.25, 0.05, 5], 4).ar;
		snd = snd ! 2;
		snd;
	}.();

	snd = (thunder * -20.dbamp);

	snd = FreeVerb.ar(in: snd, mix: 0.3, room: 1.0, damp: 0.5);

	snd;
}).play;

// Fade Out everything else
Ndef(\distant_thunder).stop(3);
Ndef(\rain).stop(3);
Ndef(\wind).stop(3);
Ndef(\junglesample).stop(3);

)


/*
__   ___.--'_`.     .'_`--.___   __
( _`.'. -   'o` )   ( 'o`   - .`.'_ )
_\.'_'      _.-'     `-._      `_`./_
( \`. )    //\`         '/\\    ( .'/ )
\_`-'`---'\\__,       ,__//`---'`-'_/
\`        `-\         /-'        '/
`                               '
*/

// PART 2 - FROGS CLUBIN'

// INSTRUCTIONS:

// 1) You will play a collective drum loop with bird noises, just listen for some time until the command is given.

// 2) Play the bird Ndef below, try not to join in all at the same time, wait a few seconds after someone joins in.

// 3) When command is given and instruments will start to fade out, one by one hit cmd + . or silence yourself with K6.
//    We want to procedually remove every bird in the drum loop, until only one remains and end there.

(
var samples;

// Only uncomment the bird sample that belongs to you. At some point you may try adding !n after
// your bird, where n is some number (like this -> \birds2!5 for example), this will create a cool effect!, also feel free to play with the rate at the bottom, but don't change the stretch parameter. Also play with the volume as you like!


// \r is a rest, don't change it

// Simoff
//samples = [\birds0, \r, \r, \r, \r, \r, \r, \r, \r];

// Kartiks
//samples = [\r, \birds2, \r, \r, \r, \r, \r, \r, \r];

// Chalsy
//samples = [\r, \r, \birds1, \r, \r, \r, \r, \r, \r];

// Mattt
//samples = [\r, \r, \r, \birds4, \r, \r, \r, \r, \r];

// Somanta
//samples = [\r, \r, \r, \r, \birds6, \r, \r, \r, \r];

// Calen
//samples = [\r, \r, \r, \r, \r, \birds8, \r, \r, \r];

// Kail
//samples = [\r, \r, \r, \r, \r, \r, \birds7, \r, \r];

// Yvi
//samples = [\r, \r, \r, \r, \r, \r, \r, \birds9, \r];

// Colorado
//samples = [\r, \r, \r, \r, \r, \r, \r, \r, \birds3];


Ndef(\bitok).quant = 4;
Ndef(\bitok,
	Pbind(
		\type, \dirt,
		\sampleName, Pseq(samples.flat, inf),
		\dur, Pseq(~dursFrom.(samples), inf),
		\stretch, 4, // don't change
		\amp, Pseq([1.0, 0.6, 0.5], inf) * 0.1, //Pwhite(0.6, 1),
		//\rate, Pwhite(0.97, 1.03, inf),
		//\rate, Pstutter(3, Pseq([1, 0.5, 2] * 0.5, inf)),
		\rate, 1 // change, try ones above
	)
).play
)
